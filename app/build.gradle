apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    buildToolsVersion '28.0.3'
    compileSdkVersion compile_sdk

    defaultConfig {
        applicationId "com.monkeyapp.numbers"
        versionCode 22
        versionName '1.0.11.201812102253'

        minSdkVersion min_sdk
        targetSdkVersion target_sdk

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [is_ocr_supported: "true"]

        resConfigs "en"
        resConfigs "nodpi"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }
    lintOptions {
        check 'NewApi', 'HandlerLeak'
        abortOnError true
    }
    dexOptions {
        javaMaxHeapSize "2048m"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation project(":appHelper")
    implementation project(':translator')
    implementation project(':ocr')

    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'androidx.core:core:1.0.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    // viewModel and liveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    // navigation
    implementation 'android.arch.navigation:navigation-fragment:1.0.0-alpha08'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "io.arrow-kt:arrow-core:$arrow_version"
    implementation 'androidx.core:core-ktx:1.0.1'

    androidTestImplementation 'androidx.arch.core:core-testing:2.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation project(':testHelper')
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.22.0'
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
}
